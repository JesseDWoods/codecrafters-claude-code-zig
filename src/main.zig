const std = @import("std");

const tool = struct { type: []const u8, function: struct { name: []const u8, description: []const u8, parameters: struct { required: []const []const u8, type: []const u8, properties: struct { file_path: struct {
    type: []const u8,
    description: []const u8,
} } } } };

pub fn main() !void {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    const args = try std.process.argsAlloc(allocator);
    defer std.process.argsFree(allocator, args);

    if (args.len < 3 or !std.mem.eql(u8, args[1], "-p")) {
        @panic("Usage: main -p <prompt>");
    }
    const prompt_str = args[2];

    const api_key = std.posix.getenv("OPENROUTER_API_KEY") orelse @panic("OPENROUTER_API_KEY is not set");
    const base_url = std.posix.getenv("OPENROUTER_BASE_URL") orelse "https://openrouter.ai/api/v1";

    // Build request body
    var body_out: std.io.Writer.Allocating = .init(allocator);
    defer body_out.deinit();
    var jw: std.json.Stringify = .{ .writer = &body_out.writer };
    try jw.write(.{
        .model = "anthropic/claude-haiku-4.5",
        .messages = &[_]struct { role: []const u8, content: []const u8 }{
            .{ .role = "user", .content = prompt_str },
        },
        .tools = &[_]tool{
            .{
                .type = "function",
                .function = .{
                    .name = "Read",
                    .description = "Read and return the contents of a file",
                    .parameters = .{
                        .type = "object",
                        .required = &[_][]const u8{"file_path"},
                        .properties = .{
                            .file_path = .{
                                .type = "string",
                                .description = "The path to the file to read",
                            },
                        },
                    },
                },
            },
        },
    });
    const body = body_out.written();

    // Build URL and auth header
    const url_str = try std.fmt.allocPrint(allocator, "{s}/chat/completions", .{base_url});
    defer allocator.free(url_str);

    const auth_value = try std.fmt.allocPrint(allocator, "Bearer {s}", .{api_key});
    defer allocator.free(auth_value);

    // Make HTTP request
    var client: std.http.Client = .{ .allocator = allocator };
    defer client.deinit();

    var response_out: std.io.Writer.Allocating = .init(allocator);
    defer response_out.deinit();

    _ = try client.fetch(.{
        .location = .{ .url = url_str },
        .method = .POST,
        .payload = body,
        .extra_headers = &.{
            .{ .name = "content-type", .value = "application/json" },
            .{ .name = "authorization", .value = auth_value },
        },
        .response_writer = &response_out.writer,
    });
    const response_body = response_out.written();

    // Parse response
    const parsed = try std.json.parseFromSlice(std.json.Value, allocator, response_body, .{});
    defer parsed.deinit();

    const choices = parsed.value.object.get("choices") orelse @panic("No choices in response");
    if (choices.array.items.len == 0) {
        @panic("No choices in response");
    }
    var ctr: usize = 0;
    while (ctr < choices.array.items.len) {
        const message = choices.array.items[ctr].object.get("message").?.object;
        // Check if there are tool calls
        if (message.get("tool_calls")) |tool_calls| {
            // Get the first tool call
            const tool_call = tool_calls.array.items[0].object.get("function").?.object;
            _ = tool_call.get("name").?.string;
            const arguments_str = tool_call.get("arguments").?.string;
            // Parse the arguments
            const args_parsed = try std.json.parseFromSlice(std.json.Value, allocator, arguments_str, .{});
            defer args_parsed.deinit();
            const file_path = args_parsed.value.object.get("file_path").?.string;
            // Execute the Read tool
            const file_contents = try std.fs.cwd().readFileAlloc(allocator, file_path, 1024 * 1024);
            defer allocator.free(file_contents);
            try std.fs.File.stdout().writeAll(file_contents);
        } else {
            // Print the message content if there are no tool calls
            const content = message.get("content").?.string;
            try std.fs.File.stdout().writeAll(content);
        }
        ctr += 1;
    }
}
